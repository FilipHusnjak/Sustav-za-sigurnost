;;-----------------------------------------------------------------------------------------
;;	FRISC-PYNQ-SYSTEM
;;	
;;	Sve informacije o nacinima spajanja i definicijama ulazno/izlaznih jedinica mozete 
;;	procitati u dokumentu:
;;		/PYNQ-LAB-STARTER/docs/PYNQ-FRISC-SYSTEM.pdf
;;
;;	Sve informacije o programiranju i konfiguriranju PYNQ plocice mozete procitati u dokumentu:
;;		/PYNQ-LAB-STARTER/docs/Upute-za-programiranje.pdf
;;
;;	Ovaj predlozak ima predefinirani skup memorijskih adresa UI jedinica.
;;	UI jedinice koje su postavljene u sustavu:
;; 		- 4 ledice i 2 RGB ledice
;;		- 4 dugmica i 2 prekidaca
;;		- 2 x 8bit GPIO (jedan ulazni i jedan izlazni)
;; 		- 2 x UART kontrolera (za bluetooth ili seriju)
;; 		- Kontroler za I2C
;;		- Kontroler za SPI
;;		- 12-bitni A/D pretvornik
;;
;;	Za sva pitanja mozete se obratiti na:
;;		alen.duspara@fer.hr
;;		tomislav.bicanic@fer.hr
;;		marko.pacadi@fer.hr
;; 		luka.macan@fer.hr
;;
;;-----------------------------------------------------------------------------------------

;; Adrese ulazno/izlaznih jedinica	
GPIO0_LEDICE    EQU	0FFF80000
GPIO0_RGB 	    EQU	0FFF80008

GPIO1_SWITCHES  EQU	0FFF81000
GPIO1_BUTTONS   EQU	0FFF81008
GPIO1_ISR	    EQU	0FFF81120
GPIO1_GIER	    EQU	0FFF8111C
GPIO1_IER	    EQU	0FFF81128

GPIO2_INPUT		EQU	0FFF82000
GPIO2_OUTPUT	EQU	0FFF82008
GPIO2_ISR	    EQU	0FFF82120
GPIO2_GIER	    EQU	0FFF8211C
GPIO2_IER	    EQU	0FFF82128

UART0_RX	    EQU 0FFF83000
UART0_TX	    EQU 0FFF83004
UART0_STAT	    EQU 0FFF83008
UART0_CTRL	    EQU 0FFF8300C

UART1_RX	    EQU 0FFF84000
UART1_TX	    EQU 0FFF84004
UART1_STAT	    EQU 0FFF84008
UART1_CTRL	    EQU 0FFF8400C

I2C_GIE			EQU 0FFF8501C 	; I2C global interrupt enable register
I2C_ISR			EQU 0FFF85020 	; I2C interrupt status register
I2C_IER			EQU 0FFF85028 	; I2C interrupt enable register
I2C_CR			EQU 0FFF85100 	; I2C control register
I2C_SR			EQU 0FFF85104 	; I2C status register
I2C_TX_FIFO		EQU 0FFF85108 	; I2C transmit FIFO
I2C_RX_FIFO	 	EQU 0FFF8510C 	; I2C recieve FIFO
I2C_SA			EQU 0FFF85110 	; I2C slave adress register (adresa na kojoj ce se odazivati - slave mode)
I2C_TX_FIFO_OCY EQU 0FFF85114 	; I2C transmit FIFO occupancy 0 = 1 podatak, 1 = 2 podatka...
I2C_RX_FIFO_OCY	EQU 0FFF85118 	; I2C recieve FIFO occupancy 0 = 1 podatak, 1 = 2 podatka...
I2C_RX_FIFO_PIR	EQU 0FFF85120 	; I2C RX FIFO - nakon koliko podataka ce se napraviti interrupt, ondosno max velicina RX_FIFO-a

SPI_CR			EQU 0FFF86060 	; SPI control register
SPI_SR			EQU 0FFF86064 	; SPI status register
SPI_DTR			EQU 0FFF86068 	; SPI data transmit register
SPI_DRR			EQU 0FFF8606C 	; SPI data receive register
SPI_SSR			EQU 0FFF86070 	; SPI Slave select register
SPI_GIER		EQU 0FFF8601C 	; SPI global interrupt enable register
SPI_IER			EQU 0FFF86028 	; SPI interrupt enable register
SPI_ISR			EQU 0FFF86020 	; SPI interrupt status register

TIMER0_CSR		EQU	0FFF87000 	; Timer 0 Control and Status Register
TIMER0_LR		EQU	0FFF87004 	; Timer 0 Load Register
TIMER0_CR		EQU	0FFF87008 	; Timer 0 Counter Register
TIMER1_CSR		EQU	0FFF87010 	; Timer 1 Control and Status Register
TIMER1_LR		EQU	0FFF87014 	; Timer 1 Load Register
TIMER1_CR		EQU	0FFF87018 	; Timer 1 Counter Register

INTC_NMI_ISR	EQU 0FFF88000   ; Interrupt status register
INTC_NMI_IAR	EQU 0FFF8800C   ; Interrupt aknowledge register
INTC_NMI_SIE	EQU 0FFF88010   ; Set interrupt enable
INTC_NMI_CIE	EQU 0FFF88014   ; Clear interrupt enable
INTC_NMI_MER	EQU 0FFF8801C   ; Master enable register

INTC_MI_ISR		EQU 0FFF89000	; Interrupt status register
INTC_MI_IAR		EQU 0FFF8900C	; Interrupt aknowledge register
INTC_MI_SIE		EQU 0FFF89010	; Set interrupt enable
INTC_MI_CIE		EQU 0FFF89014	; Clear interrupt enable
INTC_MI_MER		EQU 0FFF8901C	; Master enable register

ADC_A0			EQU	0FFF8A244	; Vrijednost A/D pretvorbe na ulazu A0


; ######### PROGRAMSKI KOD ZA "SUSTAV ZA SIGURNOST" #################
; 
; NAPISALI: filip.husnjak@fer.hr
;			luka.mesaric@fer.hr
;
; #################
;
; PIR senzor spaja se na IN[0]
; Detektor buke (mikrofon) spaja se na IN[1]
; Aktivni zvucnik spaja se na OUT[0]	
; Umjesto RTC modula koristi se ugradjeni Timer
;
; ###################################################################

				ORG 0
MAIN			MOVE 10000, SP					; inicijalizacija SP
				JR MAIN_START					; skok u glavni program


; MASKIRAJUCI PREKIDNI VEKTOR ###################

				ORG 8
				DW 3000							; adresa maskirajuceg potprograma
				
				
; PREKIDNI POTPROGRAM - NEMASKIRAJUCI ###############################

				ORG 0C
				
				PUSH  R0						; spremanje konteksta
				PUSH  R1
				MOVE  SR, R0
				PUSH  R0
				
				LOAD  R0, (GPIO1_ISR)			; testiraj je li GPIO1 izazvao NMI
				OR	  R0, R0, R0
				JR_NZ NMI_GPIO1
				
				LOAD  R0, (TIMER0_CSR)			; testiraj je li timer izazvao NMI
				AND   R0, %B 000100000000, R0
				JR_NZ NMI_TIMER
				
				JR    NMI_FULL_KRAJ				; nije bio ni timer ni GPIO1 - ne bi se smjelo dogoditi
				
; #### TIMER IZAZVAO NMI ####
				
NMI_TIMER		LOAD  R0, (TIMER0_CSR)			; clear interrupt
				OR    R0, %B 0100000000, R0
				STORE R0, (TIMER0_CSR)
				
				LOAD  R0, (GPIO2_OUTPUT)		; toggle zvucnik
				XOR   R0, 1, R0
				STORE R0, (GPIO2_OUTPUT)
				
				LOAD  R0, (ALARM_NOF_FLASHES)	; uvecaj broj flasheva
				ADD   R0, 1, R0					; uvecava se i prilikom paljenja i prilikom gasenja lampice
				STORE R0, (ALARM_NOF_FLASHES)
				
				LOAD  R1, (ALARM_NOF_FLASHES_MAX)
				CMP   R0, R1					; ako je prekoracen maksimalni broj, ugasi alarm
				CALL_EQ GASENJE_TIMERA
				JR_EQ END_NMI_TIMER
				
				LOAD  R0, (GPIO0_RGB)			; upali ili ugasi crvenu lampicu?
				AND   R0, %B 111000, R1
				JR_NZ RGB_RED_WAS_ON
				
RGB_RED_WAS_OFF	OR    R0, %B 100000, R0			; upali crvenu lampicu, ne diraj drugu
				JR    RGB_RED_CONTIN
				
RGB_RED_WAS_ON	AND   R0, %B 000111, R0			; ugasi crvenu lampicu, ne diraj drugu

RGB_RED_CONTIN	STORE R0, (GPIO0_RGB)
				
END_NMI_TIMER	MOVE  %B 01000000, R0			; MOVE 40, R0   CISTI TIMER !!!
				STORE R0, (INTC_NMI_IAR)
				JR    NMI_FULL_KRAJ
				
; #### GPIO1 IZAZVAO NMI ####
				
NMI_GPIO1		LOAD  R0, (GPIO1_ISR)			; MOVE 1, R0 ZA SWITCH	; MOVE 2, R0 ZA BUTTON
				STORE R0, (GPIO1_ISR)			; NIKAD NE RADITI MOVE 3 JER TO...
				AND   R0, 1, R0					; PROGRAMSKI GENERIRA PREKID NA DRUGOM MJESTU
				JR_Z  BUTTONS_ACTIVE
				
SWITCH_ACTIVE	LOAD  R0, (SYSTEM_STATUS)		; koje je prethodno stanje sustava?
				OR    R0, R0, R0
				JR_Z  NMI_UPALI_SUST			; ako je sustav prethodno bio ugasen, upali ga

NMI_UGASI_SUST	MOVE  0, R0
				STORE R0, (SYSTEM_STATUS)		; ugasi sustav
				STORE R0, (SYSTEM_ALARM)		; ugasi alarm
				STORE R0, (GPIO0_LEDICE)		; ugasi ledice
				MOVE  %B 100, R0				; postavi crvenu RGB lampicu
				STORE R0, (GPIO0_RGB)
				
				CALL  GASENJE_TIMERA			; ugasi timer
				
				MOVE  1, R0 					; acknowledge prekida
				STORE R0, (INTC_NMI_IAR)
				
				POP   R0						; obnavljanje konteksta
				AND   R0, %B 01111, R0			; brisanje GIE zastavice...
				MOVE  R0, SR					; kako senzori ne bi javljali prekide
				
				POP   R1
				POP   R0
				
				RETN
				
				
NMI_UPALI_SUST	LOAD  R0, (GPIO2_ISR)			; spustanje prekida senzora (GPIO2) prije paljenja sustava
				STORE R0, (GPIO2_ISR)
				
				MOVE  2, R0						; acknowledge prekida senzora (GPIO2) prije paljenja sustava
				STORE R0, (INTC_MI_IAR)
				
				MOVE  -1, R0					; enable globalnog interrupta za senzore (GPIO2)
				STORE R0, (GPIO2_GIER)

				MOVE  1, R0						; status 1/upaljeno
				STORE R0, (SYSTEM_STATUS)
				
				MOVE  %B 010, R0				; postavi zelenu RGB lampicu
				STORE R0, (GPIO0_RGB)
				
				MOVE  0, R0						; resetiraj broj flasheva
				STORE R0, (ALARM_NOF_FLASHES)
				
				MOVE  1, R0						; acknowledge prekida
				STORE R0, (INTC_NMI_IAR)
				
				POP   R0						; obnavljanje konteksta
				OR    R0, %B 10000, R0			; postavljanje GIE zastavice...
				MOVE  R0, SR					; kako bi senzori ponovno javljali prekide
				
				POP   R1
				POP   R0
				
				RETN
				
				
BUTTONS_ACTIVE	LOAD  R0, (SYSTEM_STATUS)		; ako je sustav ugasen, gumbi ne rade nista
				OR    R0, R0, R0
				JR_Z  NMI_BUTTONS_END
				
				LOAD  R0, (GPIO1_BUTTONS)		; ako je pustanje gumba generiralo prekid, ne radi nista
				OR    R0, R0, R0
				JR_Z  NMI_BUTTONS_END
	
				LOAD  R0, (SYSTEM_ALARM)		; ako je alarm ugasen, ne radi nista
				OR    R0, R0, R0
				JR_Z  NMI_BUTTONS_END
				
				CALL  GASENJE_TIMERA			; gasenje timera
				
				MOVE  -1, R0					; GPIO2 interrupt enable
				STORE R0, (GPIO2_GIER)
				
				LOAD  R0, (GPIO2_ISR)			; spustanje prekida
				STORE R0, (GPIO2_ISR)
				
				MOVE  2, R0						; acknowledge prekida
				STORE R0, (INTC_MI_IAR)
				
				MOVE  0, R0							
				STORE R0, (GPIO0_LEDICE)		; resetiranje ledica
				STORE R0, (SYSTEM_ALARM)		; resetiranje sistem alarma

NMI_BUTTONS_END MOVE  1, R0 					; %B 0001 za ciscenje GPIO1
				STORE R0, (INTC_NMI_IAR)		; AKNOWLEDGE OBAVEZNO NA KRAJ!!!

NMI_FULL_KRAJ	POP   R0						; obnavljanje konteksta
				MOVE  R0, SR
				POP   R1
				POP   R0
				
				RETN
				
				
; MAIN ##############################################################

MAIN_START 		MOVE  3, R0
				STORE R0, (INTC_NMI_MER)		; globalno omogucavanje NMI na kontroleru
				STORE R0, (INTC_MI_MER)			; globalno omogucavanje INT na kontroleru
				
				MOVE  %B 1000001, R0			; select NMI -> Timer + GPIO1
				STORE R0, (INTC_NMI_SIE)
				
				MOVE  %B 0000010, R0			; select INT -> GPIO2
				STORE R0, (INTC_MI_SIE)
				
				MOVE  1, R0
				ROTR  R0, 1, R0
				STORE R0, (GPIO1_GIER)			; globalno omogucavanje prekida na GPIO1
				STORE R0, (GPIO2_GIER)			; globalno omogucavanje prekida na GPIO2
				
				MOVE  %B 11, R0
				STORE R0, (GPIO1_IER)			; omogucavanje prekida na gumbima i switchevima
				
				MOVE  %B 01,R0
				STORE R0, (GPIO2_IER)			; omogucavanje prekida za input port
				
				LOAD  R0, (ALARM_FLASH_CONST)	; priprema timera
				STORE R0, (TIMER0_LR)
				MOVE  %B 000101110010, R0		; punjenje countera i konfiguriranje timera
				STORE R0, (TIMER0_CSR)
				MOVE  0, R0						; resetiranje broja flasheva
				STORE R0, (ALARM_NOF_FLASHES)
				
				MOVE  %B 100, R0
				STORE R0, (GPIO0_RGB)			; pocetno stanje sustava - ugaseno
				
				MOVE  %B 00000, SR 				; GIE disable jer u pocetku ne pratimo senzore
				
				
MAIN_LOOP		JR    MAIN_LOOP

				HALT
				

; PREKIDNI POTPROGRAM - MASKIRAJUCI #################################
				
				ORG 3000
				
				PUSH  R0						; spremi kontekst
				MOVE  SR, R0
				PUSH  R0
				
				LOAD  R0, (GPIO2_ISR)			; spusti prekid
				STORE R0, (GPIO2_ISR)
				
				LOAD  R0, (GPIO2_INPUT)			; testiraj je li prekid generiran zbog 0->1
				AND   R0, %B 11, R0
				JR_Z  KRAJ_INT					; na prekid 1->0 ne reagiramo
				
				MOVE  0, R0						; disable GPIO2 interrupt
				STORE R0, (GPIO2_GIER)
				
				MOVE  1, R0						; postavi alarm status
				STORE R0, (SYSTEM_ALARM)

				LOAD  R0, (TIMER0_CSR)			; rutina za aktivaciju timera (alarma)
				AND   R0, %B 111101011111, R0
				OR    R0, %B 000010000000, R0
				STORE R0, (TIMER0_CSR)
				
KRAJ_INT		MOVE  2, R0						; acknowledge prekida
				STORE R0, (INTC_MI_IAR)
				
				POP   R0						; obnavljanje konteksta
				MOVE  R0, SR
				POP   R0
				
				RETI


; POTPROGRAM GASENJE_TIMERA #########################################
				
GASENJE_TIMERA	PUSH  R0						; spremanje konteksta
				MOVE  SR, R0
				PUSH  R0

				LOAD  R0, (TIMER0_CSR)			; zaustavi timer
				AND   R0, %B 111101011111, R0
				STORE R0, (TIMER0_CSR)
				
				LOAD  R0, (GPIO0_RGB)			; ugasi crvenu lampicu, ne diraj drugu
				AND   R0, %B 000111, R0
				STORE R0, (GPIO0_RGB)
				
				MOVE  0, R0
				STORE R0, (GPIO2_OUTPUT)		; ugasi zvucnik
				STORE R0, (ALARM_NOF_FLASHES)	; resetiraj broj flasheva lampice

				POP   R0						; obnavljanje konteksta
				MOVE  R0, SR
				POP   R0
				
				RET
				
				
; POTPROGRAM WAIT_DEBOUNCE ##########################################

WAIT_DEBOUNCE	PUSH  R0						; NE KORISTI SE AKTIVNO
				LOAD  R0, (WAIT_DEBOUNCE_CONST)	; sluzi za gubitak vremena...
WAIT_DEBOUNCE_L	SUB   R0, 1, R0					; prilikom ocitavanja stanja na gumbima
				JR_NZ WAIT_DEBOUNCE_L
				POP   R0
				RET
				
				
; POTPROGRAM DEBUGGER ###############################################

DEBUGGER		PUSH  R0						; STOGOM PRIMA JEDAN PARAMETAR
				PUSH  R1						; NE KORISTI SE AKTIVNO
				MOVE  SR, R0					; spremanje konteksta i SR
				PUSH  R0
				
				MOVE  0, R0						; onemoguci senzore da ne smetaju
				STORE R0, (GPIO1_GIER)
				
				LOAD  R0, (SP+10)				; ispitivani podatak nalazi se u R0
				LOAD  R1, (GPIO0_LEDICE)		; pohranjivanje prethodnog stanja ledica
				PUSH  R1
				LOAD  R1, (GPIO0_RGB)			; pohranjivanje prethodnog stanja RGB
				PUSH  R1
				
				MOVE  %B 100001, R1				; feedback korisniku - crvena i plava lampica
				STORE R1, (GPIO0_RGB)
				
DEBUG_LOOP		STORE R0, (GPIO0_LEDICE)
				LOAD  R1, (GPIO1_BUTTONS)
				CMP   R1, %B 0001				; BTN0 za prekid debugga
				JR_EQ DEBUG_IZLAZ
				CMP   R1, %B 0010				; BTN1 za shiftanje podatka
				JR_NE DEBUG_REPEAT
DEGUB_SHIFT		SHR   R0, 4, R0					; shiftanje podatka (4 ledice)
DEBUG_REPEAT	CALL  DEBUG_WAIT				; kako bi jedan klik bio stvarno samo jedan klik
				JR    DEBUG_LOOP
				
DEBUG_IZLAZ		POP   R1						; obnavljanje starog stanja ledica i RGB
				STORE R1, (GPIO0_RGB)	 
				POP   R1
				STORE R1, (GPIO0_LEDICE)
				
				MOVE  1, R0						; ponovno omoguci senzore jer su potrebni za rad
				ROTR  R0, 1, R0
				STORE R0, (GPIO1_GIER)
				
				POP   R1						; obnavljanje konteksta i SR
				MOVE  R1, SR
				POP   R1
				POP   R0
				
				RET
				
				
; POTPROGRAM DEBUG_WAIT  ########################

DEBUG_WAIT		PUSH  R0						; NE KORISTI SE AKTIVNO
				LOAD  R0, (DEBUG_WAIT_TIME)		; vrijeme cekanja
DEBUG_WAIT_LOOP	SUB   R0, 1, R0					; gubitak vremena
				JR_NZ DEBUG_WAIT_LOOP
				POP   R0						; obnavljanje konteksta
				RET


; PODATCI U MEMORIJI ################################################

SYSTEM_STATUS 			DW 0
SYSTEM_ALARM			DW 0

ALARM_FLASH_CONST 		DW %D 4000000
ALARM_NOF_FLASHES 		DW 0					; vrijeme trajanja alarma ovisi o ALARM_FLASH_CONST
ALARM_NOF_FLASHES_MAX	DW 60					; pravi broj flasheva je duplo manji

DEBUG_WAIT_TIME 		DW %D 100000
WAIT_DEBOUNCE_CONST		DW %D 10000
